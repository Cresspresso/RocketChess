
#version 330 core

in vec3 fragPos;
in vec3 fragNormal;
in vec2 fragUV;

out vec4 color;

uniform sampler2D tex0;
uniform vec3 ambientColor = vec3(0.05f, 0.05f, 0.05f);
uniform vec3 lightColor = vec3(1,1,1);
uniform vec3 lightPos = vec3(-2, 6, 3);
uniform vec3 eye = vec3(0, 0, 10);
uniform float lightSpecularStrength = 1.0f;
uniform float shininess = 32.0f;

void main()
{
	// Light Direction
	vec3 norm = normalize(fragNormal);
	vec3 lightDir = normalize(fragPos - lightPos);

	// Diffuse Colouring
	float diffuseStrength = max(dot(norm, -lightDir), 0.0f);
	vec3 diffuse = diffuseStrength * lightColor;
	
	// Specular Highlight
	vec3 negViewDir = normalize(eye - fragPos);

		//vec3 reflectDir = reflect(lightDir, norm);
		//float specularStrength = pow(max(dot(negViewDir, reflectDir), 0.0f), shininess);

	vec3 halfwayVec = normalize(negViewDir - lightDir);
	float specularStrength = pow(max(dot(norm, halfwayVec), 0.0f), shininess);
	vec3 specular = lightSpecularStrength * specularStrength * lightColor;

	vec3 res = ambientColor + diffuse + specular;
	color = vec4(res, 1) * texture(tex0, fragUV);
}
